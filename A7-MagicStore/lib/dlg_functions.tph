INCLUDE ~%MOD_FOLDER%/lib/are_functions.tph~
INCLUDE ~%MOD_FOLDER%/lib/cre_functions.tph~

// Creates a list of dialog responses with all stores found in the game
DEFINE_ACTION_FUNCTION GENERATE_DIALOG_RESPONSES
STR_VAR
  sto_dlg_array = ~sto_dlg_array~
  dlg_cre_array = ~dlg_cre_array~
  cre_are_array = ~cre_are_array~
RET_ARRAY
  responses // associative array: %[sto resref]% => %[.d response entry]%
BEGIN
  LAF GET_ARE_NAMES RET_ARRAY are_names END

  ACTION_CLEAR_ARRAY ~responses~
  ACTION_PHP_EACH EVAL ~%sto_dlg_array%~ AS sto => dlg BEGIN
    LAF GET_STORE_NAME STR_VAR sto_resref = EVAL ~%sto%~ RET sto_name END
    ACTION_IF (~%sto_name%~ STR_EQ ~~ || ~%sto_name%~ STRING_MATCHES_REGEXP ~<Invalid Strref.*~ = 0) BEGIN
      OUTER_SPRINT sto_name @2020 // Store
    END

    OUTER_TEXT_SPRINT are ~~
    OUTER_TEXT_SPRINT are_base ~~
    OUTER_TEXT_SPRINT are_name ~~
    OUTER_TEXT_SPRINT cre ~~
    OUTER_TEXT_SPRINT cre_name ~~
    ACTION_IF (VARIABLE_IS_SET $EVAL ~%dlg_cre_array%~(~%dlg%~)) BEGIN
      OUTER_TEXT_SPRINT cre $EVAL ~%dlg_cre_array%~(~%dlg%~)
      LAF GET_CRE_NAME STR_VAR cre_resref = EVAL ~%cre%~ RET cre_name END
      ACTION_IF (VARIABLE_IS_SET $EVAL ~%cre_are_array%~(~%cre%~)) BEGIN
        OUTER_TEXT_SPRINT are $EVAL ~%cre_are_array%~(~%cre%~)
        LAF FIND_NAMED_AREA
          STR_VAR are_resref = EVAL ~%are%~
          RET are_base = resref
        END
        ACTION_IF (NOT ~%are_base%~ STR_EQ ~~) BEGIN
          OUTER_TEXT_SPRINT are_name $are_names(~%are_base%~)
        END
      END
    END

    // Skip stores in dummy areas entirely
    ACTION_IF (~%are_name%~ STR_EQ ~~ || NOT ~%are_name%~ STR_EQ ~%are_base%~) BEGIN
      // Prevent invalid strings from messing up the dialog box
      ACTION_FOR_EACH name_var IN ~sto_name~ ~cre_name~ ~are_name~ BEGIN
        OUTER_TEXT_SPRINT name EVAL ~%%name_var%%~
        ACTION_IF (STRING_LENGTH ~%name%~ > 80) BEGIN
          LAF SUBSTRING INT_VAR start = 0 length = 80 STR_VAR string = EVAL ~%name%~ RET substring END
          OUTER_TEXT_SPRINT EVAL ~%name_var%~ ~%substring%...~
        END
      END

      // generating response string
      OUTER_TEXT_SPRINT store_name ~%sto_name%~
      OUTER_TEXT_SPRINT creature_name ~%cre_name%~
      OUTER_TEXT_SPRINT area_name ~%are_name%~

      OUTER_TEXT_SPRINT key ~%sto_name%:%cre%:%are%~
      ACTION_IF (VARIABLE_IS_SET $count_array(~%key%~)) BEGIN
        OUTER_SET $count_array(~%key%~) += 1
        OUTER_SET idx = $count_array(~%key%~)
        OUTER_TEXT_SPRINT store_name ~%store_name% (%idx%)~
      END ELSE BEGIN
        OUTER_SET $count_array(~%key%~) = 1
      END

      ACTION_IF (~%area_name%~ STR_EQ ~~ && ~%creature_name%~ STR_EQ ~~) BEGIN
        OUTER_SPRINT response @2010 // "%store_name%".
      END ELSE ACTION_IF (~%area_name%~ STR_EQ ~~) BEGIN
        OUTER_SPRINT response @2011 // "%store_name%" by %creature_name%.
      END ELSE ACTION_IF (~%creature_name%~ STR_EQ ~~) BEGIN
        OUTER_SPRINT response @2012 // "%store_name%" in "%area_name%".
      END ELSE BEGIN
        OUTER_SPRINT response @2013 // "%store_name%" by %creature_name%, in "%area_name%".
      END

      // generating dialog response command
      OUTER_TEXT_SPRINT $responses(~%sto%~) ~~~~~  + ~!Global("A7STO-%sto%","GLOBAL",0)~ + ~%response%~ DO ~StartStore("%sto%",LastTalkedToBy(Myself))~ EXIT~~~~~
    END
  END
END


// Creates a list of stores opened by the current dialog file
DEFINE_PATCH_FUNCTION GET_DLG_STORES
RET_ARRAY
  stores  // associative array with key is "STO resref" and value is number of STO instances found in the DLG
BEGIN
  CLEAR_ARRAY ~stores~
  READ_ASCII 0 sig (8) NULL
  PATCH_IF (~%sig%~ STR_EQ ~DLG V1.0~) BEGIN
    READ_LONG 0x28 ofs_actions
    READ_LONG 0x2c num_actions
    FOR (i = 0; i < num_actions; ++i) BEGIN
      SET ofs = ofs_actions + (i * 8)
      READ_LONG ofs ofs_action
      READ_LONG (ofs + 4) len_action
      READ_ASCII ofs_action action (len_action)
      INNER_PATCH ~%action%~ BEGIN
        REPLACE_EVALUATE
          ~StartStore("\([^"]+\)"~
          BEGIN
            TEXT_SPRINT store ~%MATCH1%~
            TO_UPPER ~store~
            PATCH_IF (FILE_EXISTS_IN_GAME ~%store%.STO~) BEGIN
              PATCH_IF (VARIABLE_IS_SET $stores(~%store%~)) BEGIN
                SET $stores(~%store%~) += 1
              END ELSE BEGIN
                SET $stores(~%store%~) = 1
              END
            END
          END
          ~%MATCH0%~
      END
    END
  END
END
