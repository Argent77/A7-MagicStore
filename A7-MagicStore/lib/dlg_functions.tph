INCLUDE ~%MOD_FOLDER%/lib/are_functions.tph~
INCLUDE ~%MOD_FOLDER%/lib/cre_functions.tph~

// Creates a list of dialog responses with all stores found in the game
DEFINE_ACTION_FUNCTION GENERATE_DIALOG_RESPONSES
INT_VAR
  fee = 0
STR_VAR
  sto_dlg_array = ~sto_dlg_array~
  dlg_cre_array = ~dlg_cre_array~
  cre_are_array = ~cre_are_array~
RET_ARRAY
  select_responses  // associative array: %[sto resref]% => %[.d response entry]%
  store_responses   // associative array: %[state_label]% => %[.d StartStore entry]%
BEGIN
  LAF GET_ARE_NAMES RET_ARRAY are_names END

  ACTION_CLEAR_ARRAY ~select_responses~
  ACTION_CLEAR_ARRAY ~store_responses~
  OUTER_SET label_index = 0

  // loading table for special stores
  // supported game ids: bg1, sod, soa, tob, iwd
  // multiple actions or triggers can be separated by semicolon
  // string ref: prefix '@' for tra reference; prefix '#' or no prefix for game strrefs; use default table value to skip
  COPY ~%MOD_FOLDER%/2da/special.2da~ ~%MOD_FOLDER%/2da/special.2da~
    COUNT_2DA_ROWS 5 num_rows
    FOR (row = 0; row < num_rows; ++row) BEGIN
      READ_2DA_ENTRY 1 0 1 default
      READ_2DA_ENTRY row 0 5 resref
      READ_2DA_ENTRY row 1 5 game
      READ_2DA_ENTRY row 2 5 trigger
      READ_2DA_ENTRY row 3 5 action
      READ_2DA_ENTRY row 4 5 str_val
      TO_UPPER ~resref~
      TEXT_SPRINT $table_special(~%resref%~) ~%game%~
      TEXT_SPRINT $table_special(~%resref%~ ~trigger~) ~%trigger%~
      TEXT_SPRINT $table_special(~%resref%~ ~action~) ~%action%~
      TEXT_SPRINT $table_special(~%resref%~ ~message~) ~~
      PATCH_IF (NOT ~%str_val%~ STR_EQ ~%default%~) BEGIN
        PATCH_IF (IS_AN_INT ~str_val~ && str_val > 0 && str_val < NEXT_STRREF) BEGIN
          GET_STRREF str_val string
          TEXT_SPRINT $table_special(~%resref%~ ~message~) ~%string%~
        END ELSE PATCH_IF (~%str_val%~ STRING_MATCHES_REGEXP ~#[0-9]+~ = 0) BEGIN
          INNER_PATCH_SAVE str_va ~%str_val%~ BEGIN DELETE_BYTES 0 1 END
          GET_STRREF str_val string
          TEXT_SPRINT $table_special(~%resref%~ ~message~) ~%string%~
        END ELSE PATCH_IF (~%str_val%~ STRING_MATCHES_REGEXP ~@[0-9]+~ = 0) BEGIN
          INNER_PATCH_SAVE str_val ~%str_val%~ BEGIN DELETE_BYTES 0 1 END
          SPRINT string (AT str_val)
          TEXT_SPRINT $table_special(~%resref%~ ~message~) ~%string%~
        END
      END
    END
  BUT_ONLY IF_EXISTS

  // defining negative response
  ACTION_IF (fee > 0) BEGIN
    // fee required
    OUTER_TEXT_SPRINT entry ~~
    OUTER_TEXT_SPRINT entry ~~~~~%entry%  + ~!PartyGoldLT(%fee%)~ + @2003 EXIT%WNL%~~~~~ // I don't want to visit any stores right now.
    OUTER_TEXT_SPRINT entry ~~~~~%entry%  + ~PartyGoldLT(%fee%)~ + @2007 EXIT%WNL%~~~~~  // I don't have enough gold to pay your fee.
    OUTER_TEXT_SPRINT $select_responses(~~) ~%entry%~
  END ELSE BEGIN
    // free of charge
    OUTER_TEXT_SPRINT $select_responses(~~) ~++ @2003 EXIT~  // I don't want to visit any stores right now.
  END

  // defining store responses
  ACTION_PHP_EACH EVAL ~%sto_dlg_array%~ AS sto_resref => _ BEGIN
    ACTION_IF (NOT VARIABLE_IS_SET $stores(~%sto_resref%~)) BEGIN
      OUTER_SET $stores(~%sto_resref%~) = 1

      LAF GET_STORE_NAME STR_VAR sto_resref RET sto_name END
      ACTION_IF (~%sto_name%~ STR_EQ ~~ || ~%sto_name%~ STRING_MATCHES_REGEXP ~<Invalid Strref.*~ = 0) BEGIN
        OUTER_SPRINT sto_name @2020 // Store
      END

      OUTER_TEXT_SPRINT cre_resref ~~
      OUTER_TEXT_SPRINT cre_name ~~
      OUTER_TEXT_SPRINT are_resref ~~
      OUTER_TEXT_SPRINT are_base ~~
      OUTER_TEXT_SPRINT are_name ~~

      OUTER_SET dlg_num = $EVAL ~%sto_dlg_array%~(~%sto_resref%~)
      OUTER_FOR (i = 0; i < dlg_num; ++i) BEGIN
        OUTER_TEXT_SPRINT dlg_resref $EVAL ~%sto_dlg_array%~(~%sto_resref%~ ~%i%~)
        ACTION_IF (VARIABLE_IS_SET $EVAL ~%dlg_cre_array%~(~%dlg_resref%~)) BEGIN
          OUTER_SET cre_num = $EVAL ~%dlg_cre_array%~(~%dlg_resref%~)
          OUTER_FOR (j = 0; j < cre_num; ++j) BEGIN
            OUTER_TEXT_SPRINT cre_resref $EVAL ~%dlg_cre_array%~(~%dlg_resref%~ ~%j%~)
            OUTER_TEXT_SPRINT cre_name ~~
            OUTER_TEXT_SPRINT are_resref ~~
            OUTER_TEXT_SPRINT are_base ~~
            OUTER_TEXT_SPRINT are_name ~~
            LAF GET_CRE_NAME STR_VAR cre_resref RET cre_name END
            ACTION_IF (VARIABLE_IS_SET $EVAL ~%cre_are_array%~(~%cre_resref%~)) BEGIN
              OUTER_TEXT_SPRINT are_resref $EVAL ~%cre_are_array%~(~%cre_resref%~)
              LAF FIND_NAMED_AREA
                STR_VAR are_resref
                RET are_base = resref
              END
              ACTION_IF (NOT ~%are_base%~ STR_EQ ~~) BEGIN
                OUTER_TEXT_SPRINT are_name $are_names(~%are_base%~)
              END
            END
            ACTION_IF (NOT ~%are_name%~ STR_EQ ~~) BEGIN
              OUTER_SET j = cre_num
            END
          END
        END
        ACTION_IF (NOT ~%cre_name%~ STR_EQ ~~ && NOT ~%are_name%~ STR_EQ ~~) BEGIN
          OUTER_SET i = dlg_num
        END
      END

      // Skip stores in dummy areas entirely
      ACTION_IF (~%are_name%~ STR_EQ ~~ || NOT ~%are_name%~ STR_EQ ~%are_base%~) BEGIN
        // Prevent invalid strings from messing up the dialog box
        ACTION_FOR_EACH name_var IN ~sto_name~ ~cre_name~ ~are_name~ BEGIN
          OUTER_TEXT_SPRINT name EVAL ~%%name_var%%~
          ACTION_IF (STRING_LENGTH ~%name%~ > 80) BEGIN
            LAF SUBSTRING INT_VAR start = 0 length = 80 STR_VAR string = EVAL ~%name%~ RET substring END
            OUTER_TEXT_SPRINT EVAL ~%name_var%~ ~%substring%...~
          END
        END

        // generating response string
        OUTER_TEXT_SPRINT store_name ~%sto_name%~
        OUTER_TEXT_SPRINT creature_name ~%cre_name%~
        OUTER_TEXT_SPRINT area_name ~%are_name%~

        OUTER_TEXT_SPRINT key ~%sto_name%:%cre_resref%:%are_resref%~
        ACTION_IF (VARIABLE_IS_SET $count_array(~%key%~)) BEGIN
          // add suffix to multiple stores of same name from same merchant to tell apart
          OUTER_SET $count_array(~%key%~) += 1
          OUTER_SET idx = $count_array(~%key%~)
          OUTER_TEXT_SPRINT store_name ~%store_name% (%idx%)~
        END ELSE BEGIN
          OUTER_SET $count_array(~%key%~) = 1
        END

        ACTION_IF (~%area_name%~ STR_EQ ~~ && ~%creature_name%~ STR_EQ ~~) BEGIN
          OUTER_SPRINT response @2010 // "%store_name%".
        END ELSE ACTION_IF (~%area_name%~ STR_EQ ~~) BEGIN
          OUTER_SPRINT response @2011 // "%store_name%" by %creature_name%.
        END ELSE ACTION_IF (~%creature_name%~ STR_EQ ~~) BEGIN
          OUTER_SPRINT response @2012 // "%store_name%" in "%area_name%".
        END ELSE BEGIN
          OUTER_SPRINT response @2013 // "%store_name%" by %creature_name%, in "%area_name%".
        END

        // Handling special stores
        OUTER_TEXT_SPRINT trigger_extra ~~
        OUTER_TEXT_SPRINT action_extra ~~
        OUTER_TEXT_SPRINT message ~~
        ACTION_IF (VARIABLE_IS_SET $table_special(~%sto_resref%~)) BEGIN
          OUTER_TEXT_SPRINT game $table_special(~%sto_resref%~)
          ACTION_TO_UPPER ~game~
          ACTION_MATCH ~%game%~ WITH
            ~BG1~ BEGIN OUTER_SET is_game = GAME_IS ~tutu tutu_totsc bgt bgee eet~ END
            ~SOD~ BEGIN OUTER_SET is_game = GAME_INCLUDES ~sod~ END
            ~SOA~ BEGIN OUTER_SET is_game = GAME_IS ~soa tob bgt bg2ee eet~ END
            ~TOB~ BEGIN OUTER_SET is_game = GAME_IS ~tob bgt bg2ee eet~ END
            ~IWD~ BEGIN OUTER_SET is_game = GAME_IS ~iwdee~ END
            DEFAULT OUTER_SET is_game = 0
          END
          ACTION_IF (is_game) BEGIN
            OUTER_TEXT_SPRINT trigger_extra $table_special(~%sto_resref%~ ~trigger~)
            OUTER_PATCH_SAVE trigger_extra ~%trigger_extra%~ BEGIN REPLACE_TEXTUALLY ~;~ ~ ~ END
            OUTER_TEXT_SPRINT action_extra $table_special(~%sto_resref%~ ~action~)
            OUTER_PATCH_SAVE action_extra ~%action_extra%~ BEGIN REPLACE_TEXTUALLY ~;~ ~ ~ END
            OUTER_TEXT_SPRINT message $table_special(~%sto_resref%~ ~message~)
            ACTION_IF (NOT ~%message%~ STR_EQ ~~) BEGIN
              OUTER_TEXT_SPRINT message ~ (%message%)~
            END
          END
        END

        // generating dialog response command
        OUTER_TEXT_SPRINT state_label ~Store.%label_index%~
        ACTION_IF (fee > 0) BEGIN
          // multiple gold checks must be summed up to make sense
          OUTER_SET updated = 0
          OUTER_PATCH_SAVE trigger_extra ~%trigger_extra%~ BEGIN
            REPLACE_EVALUATE
              ~\(!?PartyGold[^(]*\)(\([0-9]+\))~
              BEGIN
                SET MATCH2 += fee
                SET updated = 1
              END
              ~%MATCH1%(%MATCH2%)~
          END
          ACTION_IF (NOT updated) BEGIN
            OUTER_TEXT_SPRINT trigger_extra ~!PartyGoldLT(%fee%) %trigger_extra%~
          END
          OUTER_TEXT_SPRINT action_fee ~TakePartyGold(%fee%)~
          OUTER_TEXT_SPRINT $select_responses(~%sto_resref%~) ~~~~~  + ~!Global("A7STO-%sto_resref%","GLOBAL",0) %trigger_extra%~ + ~%response%%message%.~ DO ~%action_fee%~ + %state_label%~~~~~
        END ELSE BEGIN
          OUTER_TEXT_SPRINT $select_responses(~%sto_resref%~) ~~~~~  + ~!Global("A7STO-%sto_resref%","GLOBAL",0) %trigger_extra%~ + ~%response%%message%.~ + %state_label%~~~~~
        END
        OUTER_TEXT_SPRINT $store_responses(~%state_label%~) ~~~~~  IF ~~ DO ~%action_extra% StartStore("%sto_resref%",LastTalkedToBy(Myself))~ EXIT~~~~~
        OUTER_SET label_index += 1
      END
    END
  END
END


// Creates a list of stores opened by the current dialog file
DEFINE_PATCH_FUNCTION GET_DLG_STORES
RET_ARRAY
  stores  // associative array with key is "STO resref" and value is number of STO instances found in the DLG
BEGIN
  CLEAR_ARRAY ~stores~
  READ_ASCII 0 sig (8) NULL
  PATCH_IF (~%sig%~ STR_EQ ~DLG V1.0~) BEGIN
    READ_LONG 0x28 ofs_actions
    READ_LONG 0x2c num_actions
    FOR (i = 0; i < num_actions; ++i) BEGIN
      SET ofs = ofs_actions + (i * 8)
      READ_LONG ofs ofs_action
      READ_LONG (ofs + 4) len_action
      READ_ASCII ofs_action action (len_action)
      INNER_PATCH ~%action%~ BEGIN
        REPLACE_EVALUATE
          ~StartStore("\([^"]+\)"~
          BEGIN
            TEXT_SPRINT store ~%MATCH1%~
            TO_UPPER ~store~
            PATCH_IF (FILE_EXISTS_IN_GAME ~%store%.STO~) BEGIN
              PATCH_IF (VARIABLE_IS_SET $stores(~%store%~)) BEGIN
                SET $stores(~%store%~) += 1
              END ELSE BEGIN
                SET $stores(~%store%~) = 1
              END
            END
          END
          ~%MATCH0%~
      END
    END
  END
END
