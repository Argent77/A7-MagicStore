INCLUDE ~%MOD_FOLDER%/lib/dlg_functions.tph~
INCLUDE ~%MOD_FOLDER%/lib/quests-bg1.tph~
INCLUDE ~%MOD_FOLDER%/lib/quests-sod.tph~
INCLUDE ~%MOD_FOLDER%/lib/quests-bg2.tph~
INCLUDE ~%MOD_FOLDER%/lib/quests-tob.tph~
INCLUDE ~%MOD_FOLDER%/lib/quests-iwd.tph~

// Installs items and associated quests
DEFINE_ACTION_FUNCTION INSTALL_QUESTS
INT_VAR
  enable_fee = 0
STR_VAR
  sto_dlg_array = ~sto_dlg_array~
  dlg_cre_array = ~dlg_cre_array~
  cre_are_array = ~cre_are_array~
BEGIN
  COPY ~%MOD_FOLDER%/bam/a7_icnug.bam~ ~override~
       ~%MOD_FOLDER%/bam/a7_icfrm.bam~ ~override~
       ~%MOD_FOLDER%/bam/a7_icrys.bam~ ~override~
       ~%MOD_FOLDER%/bam/a7_imbk.bam~ ~override~

  ACTION_IF (NOT GAME_IS ~iwdee~) BEGIN
    COPY ~%MOD_FOLDER%/bam/a7_ccnug.bam~ ~override~
         ~%MOD_FOLDER%/bam/a7_ccfrm.bam~ ~override~
  END

  COPY ~%MOD_FOLDER%/itm/a7_cnug.itm~ ~override~
    SAY NAME1 @3100 // Copper Nuggets
    SAY NAME2 @3100 // Copper Nuggets
    SAY UNIDENTIFIED_DESC @3101
    PATCH_IF (GAME_IS ~iwdee~) BEGIN
      WRITE_ASCII 0x58 ~~ (8) // remove description image
    END

  COPY ~%MOD_FOLDER%/itm/a7_cfrm.itm~ ~override~
    SAY NAME1 @3200 // Ornate Copper Frame
    SAY NAME2 @3200 // Ornate Copper Frame
    SAY UNIDENTIFIED_DESC @3201
    PATCH_IF (GAME_IS ~iwdee~) BEGIN
      WRITE_ASCII 0x58 ~~ (8) // remove description image
    END

  COPY ~%MOD_FOLDER%/itm/a7_crys.itm~ ~override~
    SAY NAME1 @3300 // Shiny Crystal~
    SAY NAME2 @3300 // Shiny Crystal~
    SAY UNIDENTIFIED_DESC @3301
    PATCH_IF (GAME_IS ~iwdee~) BEGIN
      WRITE_ASCII 0x58 ~~ (8) // remove description image
    END

  COPY ~%MOD_FOLDER%/itm/a7_mbk.itm~ ~override~
    SAY NAME1 @3400 // Forgotten Artifacts Vol. 3
    SAY NAME2 @3400 // Forgotten Artifacts Vol. 3
    PATCH_IF (GAME_IS ~iwdee~) BEGIN
      SAY UNIDENTIFIED_DESC @3402
      WRITE_ASCII 0x58 ~~ (8) // remove description image
    END ELSE BEGIN
      SAY UNIDENTIFIED_DESC @3401
    END

  COPY ~%MOD_FOLDER%/itm/a7_mcsum.itm~ ~override~
    SAY NAME1 @3000 // Magic Store of Vergadain
    SAY NAME2 @3000 // Magic Store of Vergadain

    SPRINT desc_abilities @3020  // Special abilities:
    SPRINT desc_ability_charges @3022  // Summon a ghostly merchant three times per day
    SAY UNIDENTIFIED_DESC @3001

    PATCH_IF (GAME_IS ~iwdee~) BEGIN
      WRITE_ASCII 0x58 ~~ (8) // description image
    END

  COPY ~%MOD_FOLDER%/bam/a7_imerc.bam~ ~override~
       ~%MOD_FOLDER%/bam/a7_cmerc.bam~ ~override~
       ~%MOD_FOLDER%/eff/a7_mcsum.eff~ ~override~
       ~%MOD_FOLDER%/itm/a7_merc1.itm~ ~override~
       ~%MOD_FOLDER%/spl/a7_merc1.spl~ ~override~
  ACTION_FOR_EACH src_file IN ~spgflsh1.vvc~ ~spgflsh1.bam~ ~eff_p64a.wav~ ~eff_m13.wav~ BEGIN
    ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%src_file%~) BEGIN
      LAF EXT_OF_FILESPEC STR_VAR filespec = EVAL ~%src_file%~ RET ext END
      COPY ~%MOD_FOLDER%/%ext%/%src_file%~ ~override~
    END
  END

  COPY ~%MOD_FOLDER%/cre/a7_merc.cre~ ~override~
    SAY NAME1 @3010 // Ghostly Merchant
    SAY NAME2 @3010 // Ghostly Merchant

  ACTION_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
    COPY ~%MOD_FOLDER%/bmp/ee/a7_mercl.bmp~ ~override~
         ~%MOD_FOLDER%/bmp/ee/a7_mercs.bmp~ ~override~
  END ELSE BEGIN
    COPY ~%MOD_FOLDER%/bmp/a7_mercl.bmp~ ~override~
         ~%MOD_FOLDER%/bmp/a7_mercs.bmp~ ~override~
  END

  // installing merchant script
  OUTER_TEXT_SPRINT A7_MERC_INIT_FEE ~~
  ACTION_IF (enable_fee) BEGIN
    LAF GET_GAMES RET_ARRAY games END
    LAF GET_FEES INT_VAR enable_fee STR_VAR games RET_ARRAY fees END
    LAF GET_GAME_CONDITIONS INT_VAR enable_fee STR_VAR games RET_ARRAY conditions END
    ACTION_PHP_EACH games AS game => available BEGIN
      ACTION_IF (available && NOT $conditions(~%game%~) STR_EQ ~~) BEGIN
        OUTER_SET fee = $fees(~%game%~)
        OUTER_TEXT_SPRINT triggers $conditions(~%game%~)
        OUTER_TEXT_SPRINT actions ~SetGlobal("A7STO-FEE","LOCALS",%fee%)~
        OUTER_TEXT_SPRINT script ~IF Global("A7STO_MERC","LOCALS",0) Global("A7STO-INIT","LOCALS",0) %triggers% THEN RESPONSE #100 SetGlobal("A7STO-INIT","LOCALS",1) %actions% END~
        OUTER_TEXT_SPRINT A7_MERC_INIT_FEE ~%A7_MERC_INIT_FEE%%script%%WNL%~
      END
    END
  END ELSE BEGIN
    // initializing dummy arrays
    OUTER_SET $games(~DEFAULT~) = 1
    OUTER_SET $fees(~DEFAULT~) = 0
    OUTER_TEXT_SPRINT $conditions(~DEFAULT~) ~~
  END
  COMPILE ~%MOD_FOLDER%/bcs/a7_merc.baf~ EVAL

  // compiling dialog
  OUTER_TEXT_SPRINT A7_MERC_INTRO_LIST ~~
  OUTER_TEXT_SPRINT A7_MERC_GAME_ENTRY ~~
  ACTION_PHP_EACH games AS game => available BEGIN
    ACTION_IF (available) BEGIN
      OUTER_SET fee = $fees(~%game%~)
      OUTER_TEXT_SPRINT triggers $conditions(~%game%~)
      ACTION_IF (fee > 0) BEGIN
        OUTER_TEXT_SPRINT A7_MERC_STATE_INTRO ~  SAY @2005~
        OUTER_TEXT_SPRINT A7_MERC_STATE ~  SAY @2006~
      END ELSE BEGIN
        OUTER_TEXT_SPRINT A7_MERC_STATE_INTRO ~  SAY @2001~
        OUTER_TEXT_SPRINT A7_MERC_STATE ~  SAY @2002~
      END

      LAF GENERATE_DIALOG_RESPONSES INT_VAR fee STR_VAR sto_dlg_array dlg_cre_array cre_are_array RET_ARRAY select_responses store_responses END
      OUTER_TEXT_SPRINT A7_MERC_RESPONSE_LIST ~~
      ACTION_PHP_EACH select_responses AS _ => response BEGIN
        OUTER_TEXT_SPRINT A7_MERC_RESPONSE_LIST ~%A7_MERC_RESPONSE_LIST%%response%%WNL%~
      END

      OUTER_TEXT_SPRINT A7_MERC_INTRO_LIST ~~~~~%A7_MERC_INTRO_LIST%  IF ~%triggers%~ DO ~SetGlobal("A7STO_MerchantSummoned","GLOBAL",1)~ + Intro.%game%%WNL%~~~~~

      OUTER_TEXT_SPRINT A7_MERC_GAME_ENTRY ~~~~~%A7_MERC_GAME_ENTRY%IF ~~ Intro.%game%
%A7_MERC_STATE_INTRO%
%A7_MERC_RESPONSE_LIST%
END

IF ~!Global("A7STO_MerchantSummoned","GLOBAL",0) %triggers% !ActuallyInCombat()~ Stores.%game%
%A7_MERC_STATE%
  COPY_TRANS SAFE ~A7_MERC~ Intro.%game%
END
~~~~~
    END
  END

  OUTER_TEXT_SPRINT A7_MERC_STORE_ENTRY ~~
  ACTION_IF (fee > 0) BEGIN
    OUTER_TEXT_SPRINT A7_MERC_STORE_STATE ~  SAY @2009~
  END ELSE BEGIN
    OUTER_TEXT_SPRINT A7_MERC_STORE_STATE ~  SAY @2008~
  END
  ACTION_PHP_EACH store_responses AS state_label => response BEGIN
    OUTER_TEXT_SPRINT A7_MERC_STORE_ENTRY ~~~~~%A7_MERC_STORE_ENTRY%IF ~~ %state_label%
%A7_MERC_STORE_STATE%
%response%
END
~~~~~
  END

  // COPY + ~%MOD_FOLDER%/dlg/a7_merc.d~ ~a7_merc.d~ EVAL
  COMPILE ~%MOD_FOLDER%/dlg/a7_merc.d~ EVAL

  // make item available
  ACTION_IF (GAME_IS ~tutu tutu_totsc bgt bgee eet~) BEGIN
    LAF INSTALL_QUEST_BG1 END
  END
  ACTION_IF (GAME_INCLUDES ~sod~) BEGIN
    LAF INSTALL_QUEST_SOD END
  END
  ACTION_IF (GAME_IS ~soa tob bgt bg2ee eet~) BEGIN
    LAF INSTALL_QUEST_BG2 END
  END
  ACTION_IF (GAME_IS ~tob bgt bg2ee eet~) BEGIN
    LAF INSTALL_QUEST_TOB END
  END
  ACTION_IF (GAME_IS ~iwdee~) BEGIN
    LAF INSTALL_QUEST_IWD END
  END
END
