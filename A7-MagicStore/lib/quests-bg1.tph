DEFINE_ACTION_FUNCTION INSTALL_QUEST_BG1
BEGIN
  ADD_JOURNAL TITLE (@10000) @10002 @10101 @10102 @10103 @10104 @10105 @10106

  COPY_EXISTING ~%NashkelMines_L1%.ARE~ ~override~
    LPF ADD_AREA_ITEM
    INT_VAR
      container_to_add_to = 5 // Container 5
      charges1 = 1
    STR_VAR
      item_to_add = ~A7_CNUG~
    END
  BUT_ONLY

  COPY_EXISTING ~%NashkelMines_L2%.ARE~ ~override~
    PATCH_FOR_EACH cnt_index IN ~3~ ~10~ ~14~ BEGIN
      LPF ADD_AREA_ITEM
      INT_VAR
        container_to_add_to = cnt_index
        charges1 = 1
      STR_VAR
        item_to_add = ~A7_CNUG~
      END
    END
  BUT_ONLY

  COPY_EXISTING ~%tutu_var%THALAN.DLG~ ~override~
    LPF GET_DLG_STORES RET_ARRAY thalan_stores = stores END
  BUT_ONLY

  ACTION_PHP_EACH thalan_stores AS sto_resref => sto_count BEGIN
    COPY_EXISTING ~%sto_resref%.STO~ ~override~
      LPF ADD_STORE_ITEM_EX
      STR_VAR
        item_name = ~A7_CRYS~
        position = ~LAST~
        flags = ~identified~
      END
    BUT_ONLY IF_EXISTS
  END

  COPY_EXISTING ~%tutu_var%BART5.DLG~ ~override~
    LPF GET_DLG_STORES RET_ARRAY bart5_stores = stores END
  BUT_ONLY

  ACTION_PHP_EACH bart5_stores AS sto_resref => sto_count BEGIN
    COPY_EXISTING ~%sto_resref%.STO~ ~override~
      LPF ADD_STORE_ITEM_EX
      STR_VAR
        item_name = ~A7_MBK~
        position = ~FIRST~
        flags = ~identified~
      END
    BUT_ONLY IF_EXISTS
  END

  EXTEND_TOP ~%Beregost_FeldepostsInn_L1_BCS%.BCS~ ~%MOD_FOLDER%/bcs/bg1_ar3351.baf~

  OUTER_TEXT_SPRINT thalan_states ~1~
  ACTION_IF (GAME_IS ~bgt~) BEGIN
    // BGT contains multiple states to patch
    OUTER_TEXT_SPRINT thalan_states ~%thalan_states% 35~
  END
  COMPILE ~%MOD_FOLDER%/dlg/dialogs_bg1.d~ EVAL
END
