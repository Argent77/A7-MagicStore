BACKUP ~weidu_external/backup/A7-MagicStore~
SUPPORT ~https://www.gibberlings3.net/forums/topic/32357-magic-store-of-vergadain-visit-any-store-anywhere-and-anytime~
VERSION ~3.0~

// Required by Tutu
ALLOW_MISSING
  ~XR2400.ARE~
  ~XR2600.ARE~

ALWAYS
  // required because of non-standard BACKUP path
  OUTER_TEXT_SPRINT MOD_FOLDER ~A7-MagicStore~
  // root folder for mutable data
  OUTER_TEXT_SPRINT WORK_FOLDER ~weidu_external~

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
    FAIL @101 // Modmerge or DLC Merger is required before mods can be installed on this game.
  END

  ACTION_DEFINE_ARRAY a7noconvert BEGIN ~setup~ END
  ACTION_DEFINE_ARRAY a7reload BEGIN ~mod~ END
  LAF HANDLE_CHARSETS
    INT_VAR
      from_utf8        = 1
      infer_charsets   = 1
    STR_VAR
      default_language = ~english~
      tra_path         = EVAL ~%MOD_FOLDER%/languages~
      out_path         = EVAL ~%WORK_FOLDER%/lang/%MOD_FOLDER%~
      noconvert_array  = ~a7noconvert~
      reload_array     = ~a7reload~
  END
END

README ~A7-MagicStore/readme/readme-%LANGUAGE%.txt~
       ~A7-MagicStore/readme/readme.txt~

LANGUAGE ~English~
         ~english~
         ~A7-MagicStore/languages/english/setup.tra~
         ~A7-MagicStore/languages/english/mod.tra~
LANGUAGE ~Deutsch~
         ~german~
         ~A7-MagicStore/languages/english/setup.tra~
         ~A7-MagicStore/languages/english/mod.tra~
         ~A7-MagicStore/languages/german/setup.tra~
         ~A7-MagicStore/languages/german/mod.tra~
LANGUAGE ~Russian (Translation: yota13)~
         ~russian~
         ~A7-MagicStore/languages/english/setup.tra~
         ~A7-MagicStore/languages/english/mod.tra~
         ~A7-MagicStore/languages/russian/setup.tra~
         ~A7-MagicStore/languages/russian/mod.tra~
LANGUAGE ~Polish (Translation: Cahir, GoForTheEyes)~
         ~polish~
         ~A7-MagicStore/languages/english/setup.tra~
         ~A7-MagicStore/languages/english/mod.tra~
         ~A7-MagicStore/languages/polish/setup.tra~
         ~A7-MagicStore/languages/polish/mod.tra~
LANGUAGE ~French (Translation: Deratiseur)~
         ~french~
         ~A7-MagicStore/languages/english/setup.tra~
         ~A7-MagicStore/languages/english/mod.tra~
         ~A7-MagicStore/languages/french/setup.tra~
         ~A7-MagicStore/languages/french/mod.tra~


BEGIN @2  // Fee required
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt bgee bg2ee eet iwdee~ @100  // This game is not supported.
SUBCOMPONENT @1 // Magic store of Vergadain
DESIGNATED 1
LABEL ~A7-MAGICSTORE-MAIN_FEE~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE ~%MOD_FOLDER%/lib/sto_functions.tph~
INCLUDE ~%MOD_FOLDER%/lib/quests.tph~

LAF INSTALL_STORES
  RET_ARRAY
    sto_dlg_array
    dlg_cre_array
    cre_are_array
END

LAF INSTALL_QUESTS INT_VAR enable_fee = 1 END


BEGIN @3  // Free of charge
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt bgee bg2ee eet iwdee~ @100  // This game is not supported.
SUBCOMPONENT @1 // Magic store of Vergadain
DESIGNATED 2
LABEL ~A7-MAGICSTORE-MAIN_NO_FEE~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE ~%MOD_FOLDER%/lib/sto_functions.tph~
INCLUDE ~%MOD_FOLDER%/lib/quests.tph~

LAF INSTALL_STORES
  RET_ARRAY
    sto_dlg_array
    dlg_cre_array
    cre_are_array
END

LAF INSTALL_QUESTS INT_VAR enable_fee = 0 END


BEGIN @10 // Alternate portrait for Ghostly Merchant
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
DESIGNATED 10
LABEL ~A7-MAGICSTORE-ALTERNATE_PORTRAIT~

ACTION_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
  COPY ~%MOD_FOLDER%/bmp/alternative/ee/a7_mercl.bmp~ ~override~
       ~%MOD_FOLDER%/bmp/alternative/ee/a7_mercs.bmp~ ~override~
END ELSE BEGIN
  COPY ~%MOD_FOLDER%/bmp/alternative/a7_mercl.bmp~ ~override~
       ~%MOD_FOLDER%/bmp/alternative/a7_mercs.bmp~ ~override~
END


BEGIN @21 // Prohibit resting
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME "ui.menu") @100 // This game is not supported.
SUBCOMPONENT @20 // Restrict available services in summoned stores
DESIGNATED 21
LABEL ~A7-MAGICSTORE-DISALLOW_RESTING~

INCLUDE "%MOD_FOLDER%/lib/ui_block_store_function.tph"

LAF ui_block_store_function
  RET inactive_strref
END

COPY_EXISTING ~a7_merc.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~StartStore~ ~SetToken("DW_SIGNAL_STORE_REST",%inactive_strref%) StartStore~
  END
BUT_ONLY


BEGIN @22 // Prohibit resting and curing
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME "ui.menu") @100 // This game is not supported.
SUBCOMPONENT @20 // Restrict available services in summoned stores
DESIGNATED 22
LABEL ~A7-MAGICSTORE-DISALLOW_RESTING_CURING~

INCLUDE "%MOD_FOLDER%/lib/ui_block_store_function.tph"

LAF ui_block_store_function
  RET inactive_strref
END

COPY_EXISTING ~a7_merc.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~StartStore~ ~SetToken("DW_SIGNAL_STORE_REST",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_CURE",%inactive_strref%)
                                    StartStore~
  END
BUT_ONLY


BEGIN @23 // Prohibit resting, curing, donating and drinking
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME "ui.menu") @100 // This game is not supported.
SUBCOMPONENT @20 // Restrict available services in summoned stores
DESIGNATED 23
LABEL ~A7-MAGICSTORE-DISALLOW_RESTING_CURING_DONATING_DRINKING~

INCLUDE "%MOD_FOLDER%/lib/ui_block_store_function.tph"

LAF ui_block_store_function
  RET inactive_strref
END

COPY_EXISTING ~a7_merc.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~StartStore~ ~SetToken("DW_SIGNAL_STORE_REST",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_CURE",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_DONATE",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_DRINK",%inactive_strref%)
                                    StartStore~
  END
BUT_ONLY


BEGIN @24 // Allow only trading
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME "ui.menu") @100 // This game is not supported.
SUBCOMPONENT @20 // Restrict available services in summoned stores
DESIGNATED 24
LABEL ~A7-MAGICSTORE-ALLOW_TRADING_ONLY~

INCLUDE "%MOD_FOLDER%/lib/ui_block_store_function.tph"

LAF ui_block_store_function
  RET inactive_strref
END

COPY_EXISTING ~a7_merc.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~StartStore~ ~SetToken("DW_SIGNAL_STORE_REST",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_CURE",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_DONATE",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_DRINK",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_IDENTIFY",%inactive_strref%)
                                    SetToken("DW_SIGNAL_STORE_STEAL",%inactive_strref%)
                                    StartStore~
  END
BUT_ONLY


BEGIN @31 // Once per day
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
SUBCOMPONENT @30 // Tweak item charges of the Magic Store (default: 3x/day)
DESIGNATED 31
LABEL ~A7-MAGICSTORE-CHARGES-ONCE_PER_DAY~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE "%MOD_FOLDER%/lib/tweak_charges.tph"

LAF tweak_charges
  INT_VAR
    charges = 1
    type    = 3 // 0: item remains, 1: item vanishes, 2: replace item, 3: item recharges
    price   = 1 * 1000
END


BEGIN @32 // 10 times per day
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
SUBCOMPONENT @30 // Tweak item charges of the Magic Store (default: 3x/day)
DESIGNATED 32
LABEL ~A7-MAGICSTORE-CHARGES-TEN_TIMES_PER_DAY~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE "%MOD_FOLDER%/lib/tweak_charges.tph"

LAF tweak_charges
  INT_VAR
    charges = 10
    type    = 3 // 0: item remains, 1: item vanishes, 2: replace item, 3: item recharges
    price   = 10 * 1000
END


BEGIN @33 // 10 charges
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
SUBCOMPONENT @30 // Tweak item charges of the Magic Store (default: 3x/day)
DESIGNATED 33
LABEL ~A7-MAGICSTORE-CHARGES-TEN_CHARGES~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE "%MOD_FOLDER%/lib/tweak_charges.tph"

LAF tweak_charges
  INT_VAR
    charges = 10
    type    = 1 // 0: item remains, 1: item vanishes, 2: replace item, 3: item recharges
    price   = 10 * 250
END


BEGIN @34 // 50 charges
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
SUBCOMPONENT @30 // Tweak item charges of the Magic Store (default: 3x/day)
DESIGNATED 34
LABEL ~A7-MAGICSTORE-CHARGES-FIFTY_CHARGES~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE "%MOD_FOLDER%/lib/tweak_charges.tph"

LAF tweak_charges
  INT_VAR
    charges = 50
    type    = 1 // 0: item remains, 1: item vanishes, 2: replace item, 3: item recharges
    price   = 50 * 250
END


BEGIN @35 // 100 charges
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
SUBCOMPONENT @30 // Tweak item charges of the Magic Store (default: 3x/day)
DESIGNATED 35
LABEL ~A7-MAGICSTORE-CHARGES-HUNDRED_CHARGES~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE "%MOD_FOLDER%/lib/tweak_charges.tph"

LAF tweak_charges
  INT_VAR
    charges = 100
    type    = 1 // 0: item remains, 1: item vanishes, 2: replace item, 3: item recharges
    price   = 100 * 250
END


BEGIN @36 // Unlimited charges
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 1 ||
                   MOD_IS_INSTALLED ~A7-MagicStore.tp2~ 2) @102 // Requires the main component.
SUBCOMPONENT @30 // Tweak item charges of the Magic Store (default: 3x/day)
DESIGNATED 36
LABEL ~A7-MAGICSTORE-CHARGES-UNLIMITED_CHARGES~

INCLUDE ~%MOD_FOLDER%/lib/init.tph~
INCLUDE "%MOD_FOLDER%/lib/tweak_charges.tph"

LAF tweak_charges
  INT_VAR
    charges = 0
    type    = 0 // 0: item remains, 1: item vanishes, 2: replace item, 3: item recharges
    price   = 6000
END
